import java.util.Properties
import java.io.FileInputStream

plugins {
    alias(libs.plugins.android.application)
}

android {
    namespace 'mindrift.app.music'
    compileSdk 34

    def keystoreProperties = new Properties()
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def hasKeystore = keystorePropertiesFile.exists()
    if (hasKeystore) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    }

    def resolveStoreFile = {
        if (!hasKeystore) return null
        def value = keystoreProperties.getProperty('storeFile')
        if (value == null || value.trim().isEmpty()) return null
        def fileRef = rootProject.file(value)
        return fileRef.exists() ? fileRef : null
    }

    defaultConfig {
        applicationId "mindrift.app.music"
        minSdk 28
        targetSdk 34
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        release {
            if (hasKeystore) {
                def storeFileRef = resolveStoreFile()
                if (storeFileRef != null) {
                    storeFile storeFileRef
                    storePassword keystoreProperties['storePassword']
                    keyAlias keystoreProperties['keyAlias']
                    keyPassword keystoreProperties['keyPassword']
                } else {
                    throw new GradleException("Keystore file not found: " + keystoreProperties.getProperty('storeFile'))
                }
            }
        }
    }

    buildTypes {
        debug {
            if (hasKeystore) {
                signingConfig signingConfigs.release
            }
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def type = variant.buildType.name == "release" ? "r" : "d"
            def version = variant.versionName ?: defaultConfig.versionName
            outputFileName = "LiSync-${type}-V${version}.apk"
        }
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.material
    
    // Core Logic Dependencies
    implementation libs.quickjs.wrapper
    implementation libs.okhttp
    implementation libs.okhttp.logging
    implementation libs.gson
    implementation libs.lifecycle.common.java8
    implementation libs.documentfile
    
    // Cache (Room for Java)
    implementation libs.room.runtime
    annotationProcessor libs.room.compiler

    // Testing
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
    
    // Xiaomi Wearable SDK
    implementation fileTree(dir: "libs", include: ["*.jar", "*.aar"])
}
